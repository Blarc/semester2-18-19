(* PODATKOVNI TIP STATUS IN STATUSNA SPOROČILA *)
type status = { (* produkt tipov *)
    version : string,
    code : int
}

val my_status : status = { version = "HTTP/1.1", code = 418 };

(* vrednost polja "version" spremenljivke "my_status": *)
#version my_status;

fun statusToString ( s : status ) : string =
    (#version s) ^ " " ^ Int.toString(#code s) ^ " " ^
    (case (#code s) of
          100 => "Continue"
        | 200 => "OK"
        | 301 => "Moved Permanently"
        | 404 => "Not Found"
        | 418 => "I'm a teapot"
        | 503 => "Service Unavailable"
        (* in še mnogo več ... *)
        | _ => "" (* neznan status *)
    )

(* DATUMI *)

type date = {
    dayOfWeek : string,
    dayOfMonth : int,
    month : string,
    year : int,
    hour : int,
    minute : int,
    second : int,
    timeZone : string
}

fun dateToString ( d : date ) : string =
    (#dayOfWeek d) ^ ", " ^
    Int.toString(#dayOfMonth d) ^ " " ^
    (#month d) ^ " " ^
    Int.toString(#year d) ^ " " ^
    Int.toString(#hour d) ^ ":" ^
    Int.toString(#minute d) ^ ":" ^
    Int.toString(#second d) ^ " " ^
    (#timeZone d)

(* URI *)

datatype host = (* vsota tipov *)
      IPV4 of int * int * int * int
    | IPV6 of string * string * string * string * string * string * string * string
    | HostName of string list

fun hostToString ( h : host ) : string =
    case h of
        IPV4 ipv4 => Int.toString(#1 ipv4) ^ "." ^
                     Int.toString(#2 ipv4) ^ "." ^
                     Int.toString(#3 ipv4) ^ "." ^
                     Int.toString(#4 ipv4)
      | IPV6 ipv6 => "[" ^
                     (#1 ipv6) ^ ":" ^
                     (#2 ipv6) ^ ":" ^
                     (#3 ipv6) ^ ":" ^
                     (#4 ipv6) ^ ":" ^
                     (#5 ipv6) ^ ":" ^
                     (#7 ipv6) ^ ":" ^
                     (#7 ipv6) ^ ":" ^
                     (#8 ipv6) ^ "]"
      | HostName hn => String.concatWith "." hn

type uri = {
    scheme : string, (* to polje je obvezno ... *)
    user : string option, (* ... ostala polja pa ne, zato dodamo "option" *)
    password : string option,
    host : host option,
    port : int option,
    path : string list option,
    query : ((string * string) list) option, (* seznam ključev in vrednosti *)
    fragment : string option
}

fun pathToString ( p : string list ) : string =
    if null p (* preverimo, če je seznam p prazen *)
    then ""
    else "/" ^ ( hd p ) ^ pathToString( tl p ) (* rekurzivna konkatenacija *)

fun queryToString ( q : (string * string) list ) : string =
    if null q
    then ""
    else (
        case q of
              a::nil => (#1 a) ^ "=" ^ (#2 a) (* v seznamu q je le en element *)
            | _ => (#1 (hd q)) ^ "=" ^ (#2 (hd q)) ^ "&" ^ queryToString( tl q )
    )

fun uriToString ( u : uri ) : string =
    (#scheme u) ^ ":" ^ (
        if isSome(#host u) (* neobvezna polja preverimo, če so nastavljena *)
        then (
            "//" ^ (
                if isSome(#user u)
                then valOf(#user u) ^ (
                    if isSome(#password u)
                    then ":" ^ valOf(#password u)
                    else ""
                ) ^ "@"
                else ""
            ) ^ hostToString(valOf(#host u)) ^ (
                if isSome(#port u)
                then ":" ^ Int.toString(valOf(#port u))
                else ""
            )
        )
        else ""
    ) ^ (
        if isSome(#path u)
        then pathToString(valOf(#path u))
        else ""
    ) ^ (
        if isSome(#query u)
        then "?" ^ queryToString(valOf(#query u))
        else ""
    ) ^ (
        if isSome(#fragment u)
        then "#" ^ valOf(#fragment u)
        else ""
    )

(* PODATKOVNI TIP RESPONSE *)

(* POLJA GLAVE *)

(* expires *)

datatype expires =
      ExpiresDate of date
      (* POZOR: ta "Date" ne sme biti poimenovan isto kot v tipu "field" *)
    | Number of int

fun expiresToString ( e : expires ) : string =
    case e of
          ExpiresDate d => dateToString(d)
        | Number s => (* lepši izpis negativnih števil *)
            if s < 0
            then "-" ^ Int.toString(~s)
            else Int.toString(s)

(* transfer encoding *)

datatype transferEncoding = chunked | compress | deflate | gzip | identity

fun transferEncodingToString ( te : transferEncoding ) : string =
    case te of
          chunked => "chunked"
        | compress => "compress"
        | deflate => "deflate"
        | gzip => "gzip"
        | identity => "identity"

(* polja glave *)

datatype field =
      Server of string
    | ContentLength of int
    | ContentType of string
    | Location of uri
    | Date of date
    | Expires of expires
    | LastModified of date
    | TransferEncoding of transferEncoding

fun fieldToString ( f : field ) : string =
    case f of
          Server s => "Server: " ^ s
        | ContentLength cl => "Content-Length: " ^ Int.toString(cl)
        | ContentType ct => "Content-Type: " ^ ct
        | Location l => "Location: " ^ uriToString(l)
        | Date d => "Date: " ^ dateToString(d)
        | Expires e => "Expires: " ^ expiresToString(e)
        | LastModified lm => "Last-Modified: " ^ dateToString(lm)
        | TransferEncoding te => "Transfer-Encoding: " ^
                                 transferEncodingToString(te)

(* response *)

type response = {
    status : status,
    headers : field list,
    body : string
}

fun responseToString ( r : response ) : string =
    statusToString(#status r) ^ "\n" ^
    (String.concatWith "\n" (map fieldToString (#headers r))) ^ "\n\n" ^
    (#body r) ^ "\n\n"

val my_response : response = {
    status = {
        version = "HTTP/1.1",
        code = 418
    },
    headers = [
        Server "gws",
        ContentLength 1024,
        ContentType "text/html; charset=UTF-8",
        Date {
            dayOfWeek = "Fri",
            dayOfMonth = 30,
            month = "Mar",
            year = 2018,
            hour = 20,
            minute = 16,
            second = 32,
            timeZone = "CEST"
        },
        (* Expires (Number ~1), *)
        Expires (ExpiresDate {
            dayOfWeek = "Thu",
            dayOfMonth = 29,
            month = "Mar",
            year = 2019,
            hour = 16,
            minute = 28,
            second = 26,
            timeZone = "CEST"
        }),
        Location {
            scheme = "http", (* obvezno polje, zato spredaj nima "SOME" *)
            host = SOME (IPV6 (
                "0000", "1234", "abcd", "01ab", "ffff", "9999", "534e", "a3f1"
            )),
            (* host = SOME (IPV4 (127, 0, 0, 1)), *)
            (* host = SOME (HostName ["google", "si"]), *)
            user = SOME "admin",
            password = SOME "ultra_safe_password_1A!",
            port = NONE, (* to polje ni podano *)
            path = SOME ["web", "page"],
            query = SOME [("a", "b"), ("x", "y")],
            fragment = SOME "top"
        },
        TransferEncoding gzip
    ],
    body = "Content of the web page."
}; (* to podpičje je obvezno, saj sledi nerezervirana beseda *)

(* prikažimo my_response *)
print(responseToString my_response);
